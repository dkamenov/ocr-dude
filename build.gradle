import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    // Apply the application plugin to add support for building a CLI application.
    id 'application'
    id 'idea'
    id "io.freefair.lombok" version "5.1.0"
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
}

dependencies {
    // This dependency is used by the application.
    implementation 'com.google.guava:guava:28.0-jre'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'

    // https://mvnrepository.com/artifact/net.sourceforge.tess4j/tess4j
    implementation group: 'net.sourceforge.tess4j', name: 'tess4j', version: '4.5.1'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'
}

application {
    // Define the main class for the application.
    mainClassName = 'com.kamenov.ocrdude.App'
}


jar {
    manifest {
        attributes(
                'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
                'Main-Class': 'com.kamenov.ocrdude.App'
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    exclude('META-INF/**', 'tessdata/**')
}

tasks.register('icons') {

    doLast {
        if (Os.isFamily(Os.FAMILY_MAC)) {
            exec {
                def inputFilepath = 'src/main/resources/images/general-ocr.png'
                def iconsetName = 'build/OCRDude.iconset'

                commandLine "sh scripts/mac_icon.sh ${inputFilepath} ${iconsetName}".split(' ')
            }
        }
    }
}


tasks.register('installer') {
    // dependencies: jpackage. on Windows: Wix; on Mac:

    dependsOn(jar, icons)
    doLast {
        exec {
            def buildNumber = System.env.BUILD_NUMBER ? System.env.BUILD_NUMBER : (new Date()).format('yyyyMMddHHmmss')
            def installerVersion="${installerVersion}.${buildNumber}"
            def copyrightDate = (new Date().format('YYYY').equals("2021") ? "2021" : "2021-" + new Date().format('yyyy'))
            def cmdLine = ["jpackage",
                "--vendor", vendor,
                "--copyright", "Â© $copyrightDate $vendor",
                "--app-version", installerVersion,
                "--dest", "build/installer/",
                "--input", "build/libs",
                "--main-jar", "ocr-dude.jar",
               "--java-options", "-Xdock:name='OCR Dude'"
            ];
            if (Os.isFamily(Os.FAMILY_MAC)) {
                cmdLine.addAll(["--icon", "build/OCRDude.icns",
                "--name", "OCR Dude"])
            } else if(Os.isFamily(Os.FAMILY_UNIX)) {
                cmdLine.addAll([
                    "--linux-package-deps", "tesseract-ocr",
                    "--linux-shortcut",
                    "--linux-app-category", "Productivity",
                    "--linux-menu-group", "Productivity",
                    "--icon", "src/main/resources/images/general-ocr.png",
                    "--name", "ocr-dude",
                ])
            } else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                cmdLine.addAll([
                    "--win-dir-chooser",
                    "--win-menu",
                    "--win-menu-group", "Productivity",
                    "--win-per-user-install",
                    "--win-shortcut",
                    "--icon", "src/main/resources/images/general-ocr.ico",
                    "--name", "OCR Dude",
                ])
            }
            commandLine cmdLine
        }
    }
}

